plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

android {
    signingConfigs {
        release {
            storeFile file("keystore.jks")
            storePassword System.getenv("SIGNING_STORE_PASSWORD")
            keyAlias System.getenv("SIGNING_KEY_ALIAS")
            keyPassword System.getenv("SIGNING_KEY_PASSWORD")
        }
    }
    namespace 'com.os.cvCamera'
    ndkVersion '27.0.12077973'

    // https://stackoverflow.com/questions/28498688/gradle-script-to-autoversion-and-include-the-commit-hash-in-android
    def gitCommitHash = 'git rev-parse --verify --short HEAD'.execute().text.trim()

    compileSdk 36

    defaultConfig {
        applicationId "com.os.cvCamera"
        minSdk 24
        targetSdk 36

        versionCode 1
        versionName "1.1.0"
        buildConfigField("String", "GIT_HASH", "\"${gitCommitHash}\"")

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                arguments "-DOpenCV_DIR=" + file('../opencvsdk4120').absolutePath + "/sdk/native/jni",
                        "-DANDROID_TOOLCHAIN=clang",
                        "-DANDROID_STL=c++_shared"
                cppFlags ''
            }
        }
    }


    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    lint {
        baseline = file("lint-baseline.xml")
        xmlOutput file('lint-results.xml')
    }


    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlin {
        jvmToolchain {
            languageVersion.set(JavaLanguageVersion.of(17))
        }
    }

    externalNativeBuild {
        cmake {
            path file('src/main/cpp/CMakeLists.txt')
            version '3.22.1'
        }
    }
    buildFeatures {
        viewBinding true
        buildConfig true
    }


    packagingOptions {
        pickFirsts += ['**/libc++_shared.so']
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include:['*.jar', '*.aar'])
    implementation 'androidx.core:core-ktx:1.16.0'
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'

    // Material
    implementation 'com.google.android.material:material:1.12.0'

    // Source - OpenCV-4 - Patched
    implementation project(path: ':opencvsdk4120')
    // Maven OpenCV-4
    // implementation 'org.opencv:opencv:4.10.0'

    //Timber Log
    implementation 'com.jakewharton.timber:timber:5.0.1'

    // AndroidX Startup
    implementation 'androidx.startup:startup-runtime:1.2.0'
    implementation 'androidx.test.ext:junit-ktx:1.2.1'
    // AndroidX Test - JVM testing
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'androidx.test.ext:junit:1.2.1'
    // Espresso UI Testing
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'



}
