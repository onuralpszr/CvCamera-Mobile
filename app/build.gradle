plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

android {

    namespace 'com.os.cvCamera'
    ndkVersion '25.2.9519653'

    // https://stackoverflow.com/questions/28498688/gradle-script-to-autoversion-and-include-the-commit-hash-in-android
    def gitCommitHash = 'git rev-parse --verify --short HEAD'.execute().text.trim()

    compileSdk 34

    defaultConfig {
        applicationId "com.os.cvCamera"
        minSdk 24
        targetSdk 34
        versionCode 1
        versionName "1.0"
        buildConfigField("String", "GIT_HASH", "\"${gitCommitHash}\"")

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                arguments "-DOpenCV_DIR=" + file('../opencvsdk480').absolutePath + "/sdk/native/jni",
                        "-DANDROID_TOOLCHAIN=clang",
                        "-DANDROID_STL=c++_shared"
                cppFlags ''
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }

    externalNativeBuild {
        cmake {
            path file('src/main/cpp/CMakeLists.txt')
            version '3.18.1'
        }
    }
    buildFeatures {
        viewBinding true
    }


    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    // Material
    implementation 'com.google.android.material:material:1.10.0'

    //OpenCV
    implementation project(path: ':opencvsdk480')

    //Timber Log
    implementation 'com.jakewharton.timber:timber:5.0.1'

    // AndroidX Startup
    implementation 'androidx.startup:startup-runtime:1.1.1'

    // Test
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}
